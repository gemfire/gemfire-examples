/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.nosphere.apache.rat" version "0.7.1"
    id "com.diffplug.spotless" version "7.0.2"
    id "de.undercouch.download" version "4.0.4"
}

if (project.getProperty('gemfireRepositoryUrl').contains('broadcom.com')) {
    if (!project.hasProperty("gemfireReleaseRepoUser") || gemfireReleaseRepoUser.isEmpty()) {
        throw new GradleException("Please set gemfireReleaseRepoUser in gradle.properties to the email address you registered at https://support.broadcom.com/")
    }
    if (!project.hasProperty("gemfireReleaseRepoPassword") || gemfireReleaseRepoPassword.isEmpty()) {
        throw new GradleException("Please set gemfireReleaseRepoPassword in gradle.properties to the registry token for $gemfireReleaseRepoUser found by navigating to https://support.broadcom.com > My Downloads > Registry Tokens")
    }
}

allprojects {
    repositories {
        if (!gradle.usingGeodeCompositeBuild) {
          maven {
            if (project.getProperty('gemfireRepositoryUrl').contains('broadcom.com')) {
              credentials {
                username = "$gemfireReleaseRepoUser"
                password = "$gemfireReleaseRepoPassword"
              }
            }
            url = project.getProperty('gemfireRepositoryUrl')
            content {
              includeGroup 'com.vmware.gemfire'
              includeGroup 'org.springframework'
            }
          }
        }
        if(project.hasProperty("useMavenLocal")) {
            mavenLocal()
        }
        mavenCentral()
    }
}

def installDir = System.getenv('GEMFIRE_HOME') ?: System.getenv('GEODE_HOME') ?: System.getProperty('GEMFIRE_HOME')

def checkEnvTask = tasks.register('checkEnv') {
    doLast {
        if (installDir==null || installDir.isEmpty()) {
            throw new GradleException("Please export GEMFIRE_HOME=<the top-level directory extracted from your GemFire .tgz> (if this message persists, you may also need to ./gradlew --stop)")
        } else {
            println("GemFire directory is $installDir")
        }
    }
}

configurations {
    compositeTarget
    gemfireDistribution
}

subprojects {
    apply plugin: 'java-library'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.compileJVMVer)
        }
    }

    dependencies {
        // All callouts to com.vmware.gemfire here, need to be specified in settings.gradle
        // for composite build to work
        api(platform("com.vmware.gemfire:gemfire-all-bom:$gemfireVersion"))
        implementation("com.vmware.gemfire:gemfire-core")
        implementation("com.vmware.gemfire:gemfire-cq")
        implementation("com.vmware.gemfire:gemfire-logging")
        implementation('com.google.guava:guava')
        implementation('org.apache.commons:commons-lang3')
        implementation("org.apache.logging.log4j:log4j-core")

        testImplementation("org.awaitility:awaitility")
        testImplementation("junit:junit")
        testImplementation("org.mockito:mockito-core")
        testImplementation("com.github.stefanbirkner:system-rules")
        testImplementation("org.assertj:assertj-core")
        testImplementation('org.apache.httpcomponents:httpcore')
        testImplementation('org.apache.httpcomponents:httpclient')
        runtimeOnly "org.apache.logging.log4j:log4j-slf4j2-impl:2+"
    }

    project.version = null
    jar {
    }

    tasks.register('cleanServer') {
        doLast {
            delete 'locator'
            delete 'locator-ln'
            delete 'locator-ln1'
            delete 'locator-ln2'
            delete 'locator-ny'
            delete 'server1'
            delete 'server2'
            delete 'server-ln-1'
            delete 'server-ln-2'
            delete 'server-ny-1'
            delete 'server-ny-2'
        }
    }
    tasks.named('clean').configure {
      finalizedBy tasks.named('cleanServer')
    }

    tasks.register('start', Exec) {
        ['build', 'cleanServer'].each {
          dependsOn tasks.named(it)
        }
        dependsOn checkEnvTask
        workingDir projectDir
        environment 'GEODE_HOME', installDir
        commandLine 'sh', '-c', "$installDir/bin/gfsh run --file=${projectDir}/scripts/start.gfsh"
    }

    tasks.register('stop', Exec) {
        dependsOn checkEnvTask
        workingDir projectDir
        environment 'GEODE_HOME', installDir
        commandLine 'sh', '-c', "$installDir/bin/gfsh run --file=${projectDir}/scripts/stop.gfsh"
    }

    tasks.register('run', JavaExec) {
        dependsOn tasks.named('build')
        description = 'Run example'
        classpath = sourceSets.main.runtimeClasspath
        mainClass = "com.vmware.gemfire.examples.${project.name}.Example"
    }

    tasks.register('waitForExitingMembers', Exec) {
        workingDir projectDir
        environment 'GEODE_HOME', installDir
        ignoreExitValue = true
        commandLine 'sh', '-c', "" +
                "TIMEOUT=120 ;" +
                "echo \"Waiting at most \$TIMEOUT seconds for all members to shut down...\" ;" +
                "while pgrep -f \"(Server|Locator)Launcher\" > /dev/null ; do" +
                "  printf \".\" ; " +
                "  sleep 1 ;" +
                "  TIMEOUT=\$((\$TIMEOUT - 1)) ;" +
                "  if [ \$TIMEOUT -eq 0 ] ; then" +
                "    echo \"\" ;" +
                "    exit 10 ;" +
                "  fi ;" +
                "done ;" +
                "echo \"\""
        doLast {
            // We use exit code 10 to avoid conflict with pgrep exit codes.
            if (executionResult.orNull.exitValue == 10) {
                throw new GradleException("A member process persisted beyond permitted timeout.  Aborting.")
            } else if (executionResult.orNull.exitValue != 0) {
                throw new GradleException("waitForExistingMembers failed with exit code: " + executionResult.orNull.exitValue)
            }
        }
    }

    tasks.register('verifyNoMembersRunning', Exec) {
        workingDir projectDir
        environment 'GEODE_HOME', installDir
        ignoreExitValue = true
        commandLine 'sh', '-c', "echo \"Looking for existing member processes...\" ; " +
                "pgrep -f \"(Server|Locator)Launcher\" ; "
        doLast {
            if (executionResult.orNull.exitValue == 0) {
                throw new GradleException("Existing members detected.  Examples expect a clean environment in which to run.")
            }
        }
    }
    if (gradle.usingGeodeCompositeBuild) {
        tasks.withType(JavaCompile) {
            options.fork = true
            options.forkOptions.jvmArgs += ['-Xmx3g']
        }
    }

    tasks.register('runAll') {
      ['verifyNoMembersRunning', 'start', 'run', 'stop', 'waitForExitingMembers'].each {
        dependsOn tasks.named(it)
      }
    }
    tasks.named('start').configure {mustRunAfter tasks.named('verifyNoMembersRunning')}
    tasks.named('run').configure {mustRunAfter tasks.named('start')}
    tasks.named('stop').configure {mustRunAfter tasks.named('run')}
    tasks.named('waitForExitingMembers').configure {mustRunAfter tasks.named('stop')}
}

apply from: "gradle/spotless.gradle"
apply from: "gradle/ide.gradle"
apply from: "gradle/rat.gradle"
